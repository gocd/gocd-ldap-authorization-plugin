/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"
apply plugin: 'java'

group = 'com.thoughtworks.gocd'

gocdPlugin {
  id = 'com.thoughtworks.gocd.authorization.ldap'
  pluginVersion = '4.2.2'
  goCdVersion = '20.9.0'
  name = 'LDAP Authorization Plugin for GoCD'
  description = 'LDAP Authorization Plugin for GoCD'
  vendorName = 'Thoughtworks, Inc.'
  vendorUrl = 'https://github.com/gocd/gocd-ldap-authorization-plugin'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'gocd-ldap-authorization-plugin'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project
  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project.tasks.findByName('jar')]
}

version = gocdPlugin.fullVersion(project)

repositories {
  mavenCentral()
  mavenLocal()
}

sourceSets {
  integrationTest {
    java.srcDirs = ['src/integration']
    resources.srcDirs += ['src/testdata']
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

configurations {
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

ext {
  deps = [
    gocdPluginApi: 'cd.go.plugin:go-plugin-api:23.2.0',
  ]

  versions = project.ext.deps.collectEntries { lib, libGav -> [lib, libGav.split(':').last()] }
}

dependencies {
  compileOnly project.deps.gocdPluginApi
  implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.13.0'

  implementation group: 'org.apache.directory.api', name: 'api-ldap-client-api', version: '1.0.3'
  constraints {
    implementation('org.apache.mina:mina-core:2.0.24') {
      because "api-ldap-client-api use an older MINA 2.0 version"
    }
  }

  testImplementation project.deps.gocdPluginApi
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.4.0'
  testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
  testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.16.1'
  testImplementation platform('org.junit:junit-bom:5.10.0')
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params'
  testImplementation 'uk.org.webcompere:system-stubs-jupiter:2.0.2'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
  testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'

  integrationTestImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.5'
  integrationTestImplementation group: 'org.apache.directory.server', name: 'apacheds-core', version: '2.0.0.AM26'
  integrationTestImplementation group: 'org.apache.directory.server', name: 'apacheds-core-annotations', version: '2.0.0.AM26'
  integrationTestImplementation group: 'org.apache.directory.server', name: 'apacheds-core-integ', version: '2.0.0-M24'
  integrationTestImplementation group: 'org.apache.directory.server', name: 'apacheds-core-jndi', version: '2.0.0.AM26'
  integrationTestImplementation group: 'org.apache.directory.server', name: 'apacheds-core-constants', version: '2.0.0.AM26'
  integrationTestRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine'
}

test {
  useJUnitPlatform()
}

task integrationTest(type: Test, description: 'Runs the ldap integration tests.') {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

  mustRunAfter tasks.test
  useJUnitPlatform()
}

check.dependsOn integrationTest

jar {
  from(configurations.runtimeClasspath) {
    into "lib/"
  }
}
